#### groovy 列表的定义
``` java
// def list = new ArrayList() // java方式，定义list列表

// groovy的方式定义list列表
def list = [1, 2, 3]
println list.class // java.util.ArrayList
println(list.size()) // 3

// 定义数组
def array = [1, 2, 3] as int[] // 通过as关键字把list专为数组
println(array.class)
int[] array2 = [1, 2, 3] // 不使用def定义变量，而是用int[] 强类型定义数组
println(array2.class)
```

#### groovy map的定义
``` java
//def map=new HashMap()// java方式定义map，
def colors = [red  : 'ff0000',
              green: '00ff00',
              red  : '0000ff']
// 索引方式取值
println(colors['red']) // 取值方式一
println(colors.red)// 取值方式二

// 添加元素
colors.yellow = 'ffff00'// 如果map中原来有key为yellow的，则更新值，否则直接添加
println(colors.toMapString())

// 给map添加数组
colors.complex = [a: 1, b: 2]
println(colors.toMapString())
println(colors.getClass()) // java.util.LinkedHashMap
// 这里不能通过colors.class 方式取获取map的类型，这样只会取map中取key为class的值，而不是获取类型
// 所以这里map获取类型只能通过getClass方法

// 总结：
// 1. map中的key值，如果是字符传类型，则尽量使用单引号定义，单引号字符串不可变
// 2. 如果我们key值是字符串，但我们没有加单引号，就如上例子，groovy会自动帮我们加上单引号
// 3. 通过def定义的map对象，默认是 java.util.LinkedHashMap 类型
// 4. 如果希望使用其他的map类型，则可以通过as关键字强转或者通过强类型定义变量
// 如：
HashMap hashMap = [red: '00ff00']
def hashTable = [red: '00ff00'] as Hashtable
```

#### map的操作详解
``` java
/*****map的操作详解****/

def students = [1: [number: '001', name: 'bob', score: 55, sex: 'male'],
                2: [number: '002', name: 'John', score: 62, sex: 'female'],
                3: [number: '003', name: 'Carry', score: 73, sex: 'female'],
                4: [number: '004', name: 'Tom', score: 66, sex: 'male'],]

// 1.map的遍历
// each遍历
students.each { student ->
    println("key :${student.key} value:$student.value")
}

// each遍历，带索引的遍历
students.eachWithIndex { def student, int index ->
    println("index:${index} key :${student.key} value:$student.value")
}

// each遍历，直接遍历key和value
students.each { key, value ->
    println("key:$key value:$value")
}

// each遍历，直接遍历key和value,带索引
students.eachWithIndex { key, value, int index ->
    println("index:$index key:$key value:$value")
}

println('开始照片\n')
// 2。map的查找,第一个及格的学生信息
println students.find { student ->
    student.value.score > 60
}

// 查找所有及格的学生信息
def entrys = students.findAll { student ->
    student.value.score > 60
}
println entrys.toMapString()

// 统计
def count = students.count { student ->
    student.value.score > 60
}
println "及格学生的人数：$count"

// 输出所有及格学生的名字
def names = students.findAll { student ->
    student.value.score > 60
}.collect { student ->
    student.value.name
}
println names

// 对map数据进行分组
def groupStudent = students.groupBy { student ->
    student.value.score > 60 ? '及格' : '不及格'
}
println groupStudent

// 对map数据进行排序
def sortStudents = students.sort { student1, student2 ->
    Number score1 = student1.value.score
    Number score2 = student2.value.score

    score1 == score2 ? 0 : score1 < score2 ? -1 : 1
}
println sortStudents
```


## groovy中的特殊数据结构：范围
``` java
def range = 1..10 // 范围的定义
println range[1]// 索引取值
println range.contains(10)// 是否包含
println range.from// 起始值
println range.to// 终止值
println(range.class) // groovy.lang.IntRange

// 遍历
range.each {
    println it
}

// 循环遍历 ，推荐第一种
for (i in range) {
    println(i)
}

def getGrade(Number score) {
    def result
    switch (score) {
        case 0..<60:// 小于号表示不包含60
            result = '不及格'
            break
        case 60..<70:
            result = '及格'
            break
        case 70..<80:
            result = '良好'
            break
        case 80..<90:
            result = '优秀'
            break
        case 90..100:
            result = '非常优秀'
            break
        default:
            break
    }
    return result
}

println(getGrade(75))
```
