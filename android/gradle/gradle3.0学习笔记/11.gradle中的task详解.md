### 一、task 定义

1. **使用project的task() 方法，创建task**

**task + 任务名称{}**

``` java
task helloTask {
  println 'i am helloTask'
}
```

2. **通过project 的tasks对象**（类型：TaskContainer）的create方法创建,即TaskContainer的create方法

``` java
this.tasks.create(name:'helloTask2'){
  println 'i am helloTask2'
}
```

### 二、task的配置信息
task 配置信息的代码，在gradle的配置阶段执行
1. task可以配置的信息如下：
| 字段名      | 描述                         |
| ----------- | ---------------------------- |
| name        | task任务名称                 |
| description | task描述信息，类似与类的注解 |
| group       | task的分组                   |
| type        | task的类型                   |
| dependsOn   | 指定task的依赖，依赖哪些task |
| overwrite   | 重写相关task                 |
| action      | 配置task执行的逻辑           |

2. **在创建task的时候，就指定task的配置信息**

``` java
task helloTask(group:'mytask',description:'task study') {
  println 'i am helloTask'
}
```

3. **在配置代码块中，调用task的各种配置方法**
``` java
task helloTask {
  setGroup('mytask') // 为task分组，进行分组管理
  setDescription('task study') // 为task添加注释，描述task的指责
  println 'i am helloTask'
}
```



### 三、指定task任务代码执行阶段



#### 1、为task添加Action

每个task中可以包含**0**到**多个**Action，这些Action保存在一个**ArrayList**成员变量中，当执行这个**task**时，会按照顺序依次执行所有的**Action**。



要为**task**添加**Action**，可以通过**task**的**doFirst()**和**doLast()**方法来实现。**doFirst()**方法会将**Action**添加到**ArrayList**的开头，而**doLast()**方法则将**Action**添加到**ArrayList**的末尾。



可以有多个**doFirst()**和**doLast()**，配置**task**时会按照它们出现的先后顺序添加到**Action**列表中。



**区别：**

通过**doFirst** 和**doLast** 指定task代码在**gradle**执行阶段运行，也只有这两个方法可以做到。
* **doFirst**   在已有task执行之前执行

* **doLast**    在已有task执行完成之后执行

  

#### 2、在task内部指定代码的执行阶段

``` java
task helloTask(group:'mytask',description:'task study') {
  println 'i am helloTask' // 在配置阶段执行

  doFirst { // 在已有task执行之前执行
    println 'the task group is:'+group
  }

  doFirst{} // 可以有多个，多个按顺序执行

  doLast { // 在已有task执行完成之后执行

  }
}
```
#### 3、在task外部指定代码的执行阶段

在task外部指定的代码块，会优先与在task内部指定的代码块而被执行
``` java
helloTask.doFirst {
  println 'the task description is :' + description
}
```
写一个计算gradle build阶段耗时时长的方法

``` java
def startBuildTime, endBuildTime
this.afterEvaluate { Project project ->
  // 找到build之前执行的task：preBuild
    def preBuildTask = project.tasks.getByName('preBuild')
    preBuildTask.doFirst { // 为该task增加doFirst执行代码块
        startBuildTime = System.currentTimeMillis()
        println "the start time is: $startBuildTime"
    }
// 找到build task
    def buildTask = project.tasks.getByName('build')
    buildTask.doLast {// 为build task 添加doLast 执行代码块，在build 任务执行结束之后执行
        endBuildTime = System.currentTimeMillis()
        println "the build time is:${endBuildTime - startBuildTime}"
    }
}
```
gradle 执行build的时候，首先执行的就是preBuild 任务，最后 执行的是 build 任务

#### 4、通过追加符：<<（等同与doLast），为task添加执行代码块

``` java
task lib1 << {
  println 'lib1' // 这行代码等同于在doLast中执行
}
```

不过这种写法目前已被标记为deprecated，预期会在Gradle 5.0版本中移除，所以最好不要再使用了



#### 

### 四、task 执行顺序

gradle中，有三种方式指定task的执行顺序
* **dependsOn，finalizedBy强依赖方式指定**
* **通过Task输入输出指定**
* **通过API指定执行顺序**

#### 4-1、通过dependsOn，finalizedBy强依赖方式指定
为task添加依赖，有两种方式：
* **静态指定依赖**：在创建task的时候指定，即明确的知道定义的task需要依赖的task是什么的时候，直接指定

  task提供了dependsOn，finalizedBy方法来管理task之间的依赖关系，依赖关系表达的是执行这个task时所需要依赖的其他task，也就是说这个task不能被单独执行，执行这个task之前或之后需要执行另外的task。

* **动态指定依赖**：在创建task的时候，不知道需要依赖哪些task，通过dependsOn方法动态依赖，符合条件的task

##### 4-1-1、静态指定依赖

``` java
task taskX{
  doLast{
    println 'taskX'
  }
}

task taskY{
  doLast{
    println 'taskY'
  }
}

task taskZ(dependsOn:taskX){ // 多依赖方式：dependsOn:[taskX,taskY]
  doLast{
    println 'taskZ'
  }
}

// 当我们执行taskZ的时候，由于依赖了taskX，则taskX会先执行，然后才会执行：taskZ


```
除了在task定义的时候，指定task的依赖之外，还可以通过**task**的**dependsOn**方法，为task指定依赖
``` java
// 通过task的dependsOn方法，也可以指定task的依赖task。
taskX.dependsOn(taskX,taskY)
```
当一个task依赖多个task的时候，被依赖的task之间，如果没有依赖关系的话，那么他们的执行顺序是随机的，并无影响。

##### 4-1-2、动态添加依赖

``` java
task lib1 {
  doLask{
    println 'lib1'
  }
}
task lib2 {
  doLask{
    println 'lib2'
  }
}
task lib3 {
  doLask{
    println 'lib3'
  }
}

// 动态指定taskX依赖所有以lib开头的task
task taskX{
  // 动态指定依赖
  dependsOn this.tasks.findAll{ task->
    return task.name.startsWidth('lib')
  }
  doLast {
    println 'taskZ'
  }
}
```


##### 4-1-3、dependsOn 与finalizedBy的区别

* **在一个task前执行另一个task**

  要在一个task之前执行另一个task，可以通过配置**dependsOn**的依赖关系实现。

  例如有两个task：**taskA**和**taskB**，通过指定**taskA.dependsOn taskB**就可以让在执行**taskA**之前先执行taskB，或者指定**taskB.dependsOn taskA**就可以让在执行**taskB**之前先执行**taskA**。****

* **在一个task后执行另一个task**

  要在一个task之后执行另一个task，无法通过配置dependsOn实现。要实现这个功能需要通过**finalizedBy**来实现。

  同样的有两个task：**taskA**和**taskB**，通过指定**taskA.finalizedBy taskB**就可以让在执行**taskA**之后执行**taskB**，或者指定**taskB.finalizedBy taskA**就可以让在执行**taskB**之后在执行**taskA**。



#### 4-2、通过Task输入输出指定

当一个参数作为TaskA的输出参数，却作为TaskB的输入参数。那么当执行TaskB的时候先要执行TaskA。即输出的Task先于输入的Task执行。
输入方式：

* TaskInputs property(String name, Object value);
* TaskInputs properties(Map<String, ?> properties);

``` java

ext {
    versionName = '1.1.0'
    versionCode = 101
    versionInfo = '第2个稳定版本'

    // 定义版本信息保存的文件
    destFile = file('release.xml')
    if (destFile != null && !destFile.exists()) {
        destFile.createNewFile()
    }
}

// 定义个把版本信息写入到文件中的task
task writeTask {
    // 指定task的输入信息
    inputs.property('versionName', this.versionName)
    inputs.property('versionCode', this.versionCode)
    inputs.property('versionInfo', this.versionInfo)

    outputs.file this.destFile

    // 把版本信息写入到xml文件中
    doLast {
        // 获取到输入信息
        def versionData = inputs.getProperties()
        def file = outputs.getFiles().getSingleFile()

        def sw = new StringWriter();
        def xmlBuilder = new groovy.xml.MarkupBuilder(sw)
        // 文件是空的
        if (file.text != null && file.text.size() <= 0) {
            // 通过MarkupBuilder把数据以xml的格式，写入到StringWriter中
            xmlBuilder.releases {
                release {
                    versionName(versionData.versionName)
                    versionCode(versionData.versionCode)
                    versionInfo(versionData.versionInfo)
                }
            }
            // 把数据写入到文件中
            file.withWriter { writer ->
                writer.append(sw.toString())
            }
        } else { // 如果文件已经有了内容，则追加
            def lines = file.readLines() // 读取文件所有行
            def lastLine = lines.size() - 1; // 文件最后一行

            xmlBuilder.release {
                versionName(versionData.versionName)
                versionCode(versionData.versionCode)
                versionInfo(versionData.versionInfo)
            }

            // 遍历每一行，并写入到文件中

            file.withWriter { writer ->
                lines.eachWithIndex { String line, int index ->
                    if (index != lastLine) {// 如果不是最后一行，直接写入
                        writer.append(line).append("\r\n") // 每一行写完，换行
                    } else { //如果是最后一行，则先把我们新增加的内容写入，在写入最后一行
                        writer.append(sw.toString()).append("\r\n")
                        writer.append(line).append('\r\n')
                    }
                }
            }

        }
    }
}

// 读文件task
task readTask {
    inputs.file this.destFile

    doLast {
        def file = inputs.getFiles().getSingleFile()
        println file.text // 把文件内容全部输出
    }
}

task readWriterTest(dependsOn: [writeTask, readTask]) {
    doLast {
        println('输入输出任务结束。。。')
    }
}
```

文件destFile是writeTask的输出参数，是readTask的输入参数，所以writeTask优先于readTask执行。



#### 4-3、通过API指定执行顺序

1. 通过**mustRunAfter** 指定task执行顺序，类似的还有**shouldRunAfter**方法，表示不强制，不常用，一般都是用mustRunAfter方法
``` java
task taskX {
    doLast {
        println 'taskX'
    }
}

task taskY {
    mustRunAfter taskX
    doLast {
        println 'taskY'
    }
}

task taskZ {
    mustRunAfter taskY
    doLast {
        println 'taskZ'
    }
}
```





```groovy
task uploadMe {
    group 'upload'
    doLast {
        println "uploadMe任务"
    }
}

task uploadYou {
    group 'upload'
    doLast {
        println "uploadYou任务"
    }
}

afterEvaluate {
    // uploadYou任务执行之后，执行uploadMe任务
    uploadYou.finalizedBy uploadMe
}
```



### 5、在一个task中执行另外一个task任务



gradle在执行一组task的时候会根据他们的依赖关系生成一个task序列，然后按照序列的先后顺序来依次执行各个task。单个task的执行一定是一个原子过程，gradle不允许在一个task中执行另一个task，因此不会出现嵌套执行的情况。



也就是说所有的task执行都是由gradle自身去调度，只有执行完一个task之后才会去执行下一个task，不会出现task没有执行完就转去执行另一个task中的任务的情况。



值得一提的是，现有gradle版本中为task保留了一个**execute()**方法来执行一个task，这个方法已被标记为**deprecated**，不过还是可以用的，但会出现异常。最好就不要用**execute()**了。



但可以用下面的方式实现：

**通过命令去在task任务内部去执行另外一个task任务**

```groovy
// 找到要执行的task任务
def uploadArchives = project.tasks.findByName("uploadArchives")
println "路径：${uploadArchives.getPath()}" // 任务路径
exec {
    // 通过命令，执行task任务，
  	// 因为gradlew命令放在工程的根目录下，第二个参数：需要执行的task任务路径，如：:libbase:uploadArchives
  // 这里最好是加上模块名称，指定执行那个模块的打包任务
    commandLine "${rootProject.projectDir}/gradlew", "${uploadArchives.getPath()}"
    println("${project.getName()}：打包成功")
}
```





### 参考博客：

* [Android Gradle Task详解](https://blog.csdn.net/ccpat/article/details/89342198)