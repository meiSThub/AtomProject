在实际项目开发中，经常需要编写一些脚本，来帮助我们实现一些繁琐的重复工作。下面是自己编写的一个脚本，主要功能是：自动更新版本，修改依赖，打包，和git提交等。
下面在实现过程中遇到的一些问题记录。



工程的基本结构如下：

![image-20201223102527682](https://raw.githubusercontent.com/meiSThub/BlogImage/master/2020/image-20201223102527682.png)

libbase是接口层，libstable和libbeta是依赖于libbase的实现层，libcontroller是依赖上面三个库的，用于灰度控制。



如果libbase，libstable，libbeta库有更新的话，libcontroller也要更新，即这个时候就需要对这四个库都发一个新版本，这样每次要修改四个文件，发四次包，比较繁琐。



所以才有了想写一个脚本来实现上面一系列的操作的想法，具体的实现步骤为：

1. 更新版本号：首先读取所有依赖库的maven.gradle文件，拿到versionCode，并自增
2. 基础库发包：版本号自增后，执行uploadArchives任务，发包

3. 更新controller依赖库的版本：所有依赖库发完包后，修改controller库的build.gradle文件，修改依赖库版本号，使用新的版本号

4. 修改controller库的maven.gradle 的版本号，并发包
5. 提交所有的更新记录



下面就记录了在实现过程中所遇到的一些难点问题：



#### 1、更新一个文件的内容：

本来想着能获取到指定的行然后更新，但暂时没有找到什么合适的方式。

目前的实现方式：把文件的内容以行的方式读到内存中，然后遍历每一行，如果是自己要更新的那一行，则更新这一行的内容，并写入到原文件中；如果不是要更新的内容，则默认不做任何操作，也写入到原文件中，这样就做到了文件内容的更新。

```groovy
/**
 * 更新库的依赖
 * @param project 需要修改依赖的工程对象
 * @param oldDependence 老版本，如：com.mei.test:upload:1.0.0
 * @param newDependence 新版本，如：com.mei.test:upload:1.0.1
 * @return
 */
def updateDependencies(Project project, String oldDependence, String newDependence) {
    // 获取build.gradle文件
    File file = file("$project.projectDir/build.gradle")
    // 遍历build.gradle文件，并更新依赖库的版本
    def lines = file.readLines()
    file.withWriter { writer ->
      // 遍历文件的每一行
        lines.eachWithIndex { String line, int lineIndex ->
          // 如果不是第一行，则追加换行符
            if (lineIndex != 0) {
                writer.write("\n")
            }
// 如果是以api开头，则表示的是依赖行，如：api 'com.mei.test:upload:1.0.0',可根据自己的依赖方式过滤，如：implementation
            if (line.trim().startsWith("api")) {//  如果是以api开头的行
                if (line.contains(oldDependence)) {// 如果是指定的库的这一行，则更新这一行的内容
                  line = line.replace(oldDependence, newDependence)// 更新这一行的内容
                }
            }
            writer.write("$line")// 重新写入文件中
        }
    }
}
```



#### 2、在一个gradle的task任务中，执行另外一个task任务



gradle在执行一组task的时候会根据他们的依赖关系生成一个task序列，然后按照序列的先后顺序来依次执行各个task。



单个task的执行一定是一个原子过程，gradle不允许在一个task中执行另一个task，因此不会出现嵌套执行的情况。也就是说所有的task执行都是由gradle自身去调度，只有执行完一个task之后才会去执行下一个task，不会出现task没有执行完就转去执行另一个task中的任务的情况。



虽然现有gradle版本中为task保留了一个**execute()**方法来执行一个task，这个方法已被标记为**deprecated**。虽然还可以用，但在执行的时候就会发现执行出错，无法继续。



所以这导致在一个task中去执行另外一个task变得困难。



这里想到了**两种实现方式**：

1. **通过依赖的方式，把需要执行的task任务串起来，让相关的任务按照我们指定的顺序去执行**，task串行之后结构如下：

   ![image-20201223105441909](https://raw.githubusercontent.com/meiSThub/BlogImage/master/2020/image-20201223105441909.png)

   这种方式相对繁琐，实现过程中，发现打包没有成功，暂时也没有找到具体的原因。但理论上是可以实现的，以后在深入研究。

2. **通过命令去在task任务内部去执行另外一个task任务**

   ```groovy
   // 找到要执行的task任务
   def uploadArchives = project.tasks.findByName("uploadArchives")
   println "路径：${uploadArchives.getPath()}" // 任务路径
   exec {
       // 通过命令，执行task任务，
     	// 因为gradlew命令放在工程的根目录下，第二个参数：需要执行的task任务路径，如：:libbase:uploadArchives
     // 这里最好是加上模块名称，指定执行那个模块的打包任务
       commandLine "${rootProject.projectDir}/gradlew", "${uploadArchives.getPath()}"
       println("${project.getName()}：打包成功")
   }
   ```

   这种方式实现简单，最终使用的也是这种方式。



#### 3、执行git相关命令



在gradle中，执行命令，也有三种方式，可以参考文章：**12.gradle执行命令**



这里使用的就是第一种方式：**命令字符串执行**。如：

```groovy
/**
 * 提交更新
 * @return
 */
def commitUpdate() {
    println "git start"
    ext.env = System.getenv()
    def curBranchName = env.GIT_LOCAL_BRANCH
    def originBranchName = env.GIT_BRANCH
    def originUrl = env.GIT_URL
    println "curBranchName:" + curBranchName
    println "originBranchName:" + originBranchName
    println "originUrl:" + originUrl
    //这个是app目录
    println "path：${rootDir}"

    //定义Git语句
    def statusPro = "git status".execute() // 执行命令字符串
    //打印Git命令的结果，一开始就是不知道有这个，执行不成功也没有日志输出，走了很多弯路
    println "git status:\n" + statusPro.inputStream.text
    statusPro.waitFor()// 等待命令执行完成

    def pullPro = "git pull".execute()
    println "git pull:\n" + pullPro.inputStream.text
    pullPro.waitFor()

    def addPro = "git add ${rootDir}/*.gradle".execute()
    println "git add:\n" + addPro.inputStream.text
    addPro.waitFor()

    def commitPro = 'git commit -m [发版更新]升级版本'.execute()
    println "git commit:\n" + commitPro.inputStream.text
    commitPro.waitFor()

    def pushPro = "git push origin ${curBranchName}".execute()
    println "git push:\n" + pushPro.inputStream.text
    pushPro.waitFor()

    def statusPro2 = "git status".execute()
    println "git status:" + statusPro2.inputStream.text
    statusPro2.waitFor()

    println "git end"
}
```



在执行命令字符串的时候，会返回一个**Process**对象，根据这个对象，我们可以拿到命令执行的日志输出，并打印到控制台上。

以及等待命令执行完成的操作，这样就能保证命令的执行是可靠的。



#### 4、完整源码：

解决上面三个问题后，自动打包程序也就基本可以实现了，完整的源码如下：

```groovy
// 1.首先读取所有依赖库的maven.gradle文件，拿到versionCode，并自增
// 2.版本号自增后，执行uploadArchives任务，发包
// 3.所有依赖库发完包后，修改controller库的build.gradle文件，修改版本号
// 4.修改controller库的maven.gradle 的版本号，并发包

def controller = 'libcontroller'
def moduleList = ["libbase", 'libstable', 'libbeta']
// def moduleList = ["libbase"]
def currentBranchName = getCurrentBranch()

/**
 * 获取
 * @return
 */
def getCurrentBranch() {
    def branch = 'git symbolic-ref --short -q HEAD'.execute()
    def name = branch.text
    println("current branch name:$name")
    branch.waitFor()
}

// 更新版本号任务
task uploadAllModule {
    group('upload')
    doLast {
        // 找到controller工程
        def controlProject = rootProject.allprojects.find { project ->
            controller.equalsIgnoreCase(project.getName())
        }
        println "controlProject:${controlProject.getName()}"

        def dependenceMap = new HashMap()

        // 1. 先发基础包
        rootProject.allprojects.each { project ->
            def projectName = project.getName()
            println "projectName:$projectName"
            if (!(projectName in moduleList)) {
                return
            }
            // 获取module的push 发包相关信息
            def pushInfo = project.extensions.pushInfo
            def groupId = pushInfo.groupId
            def artifactId = pushInfo.artifactId
            def versionCode = pushInfo.versionCode

            // 基础库发包
            def newVersionCode = uploadModuleArchives(project)
            println "版本更新成功：$newVersionCode"

          	// 旧版本，如：com.mei.test:upload:1.0.0
            def oldDependence = "$groupId:$artifactId:$versionCode" 
            println "oldDependence=$oldDependence"
          	// 新版本，如：com.mei.test:upload:1.0.1
            def newDependence = "$groupId:$artifactId:$newVersionCode"
            println "newDependence=$newDependence"
            dependenceMap[oldDependence] = newDependence
        }

        // 2.更新controller库依赖库的版本
        updateDependencies(controlProject, dependenceMap)

        // 3.controller库发包
        uploadModuleArchives(controlProject)

        // 4.git 提交版本更新
        commitUpdate()
    }
}

/**
 * 提交更新
 * @return
 */
def commitUpdate() {
    println "git start"
    ext.env = System.getenv()
    def curBranchName = env.GIT_LOCAL_BRANCH
    def originBranchName = env.GIT_BRANCH
    def originUrl = env.GIT_URL
    println "curBranchName:" + curBranchName
    println "originBranchName:" + originBranchName
    println "originUrl:" + originUrl
    //这个是app目录
    println "path：${rootDir}"

    //定义Git语句
    def statusPro = "git status".execute()
    //打印Git命令的结果，一开始就是不知道有这个，执行不成功也没有日志输出，走了很多弯路
    println "git status:\n" + statusPro.inputStream.text
    statusPro.waitFor()

    def pullPro = "git pull".execute()
    println "git pull:\n" + pullPro.inputStream.text
    pullPro.waitFor()

    def addPro = "git add ${rootDir}/*.gradle".execute()
    println "git add:\n" + addPro.inputStream.text
    addPro.waitFor()

    def commitPro = 'git commit -m [发版更新]升级版本'.execute()
    println "git commit:\n" + commitPro.inputStream.text
    commitPro.waitFor()

    def pushPro = "git push origin ${curBranchName}".execute()
    println "git push:\n" + pushPro.inputStream.text
    pushPro.waitFor()

    def statusPro2 = "git status".execute()
    println "git status:" + statusPro2.inputStream.text
    statusPro2.waitFor()

    println "git end"
}

/**
 * 为指定module，发新包
 * @param project 指定的module
 * @return 返回新的版本号
 */
def uploadModuleArchives(Project project) {
    // 1. 修改工程版本
    def newVersionCode = getNewVersionCode(project)
    println "newVersionCode=$newVersionCode"
    if (newVersionCode == null) {
        return
    }

    println "工程路径：$projectDir"
    // 2. 把修改的版本号，更新到maven.gradle文件中
    updateMavenFile(project, newVersionCode)

    // 3. 发布新的版本
    def uploadArchives = project.tasks.findByName("uploadArchives")
    println "路径：${uploadArchives.getPath()}"
    exec {
        // 通过命令，执行task任务
        commandLine "${rootProject.projectDir}/gradlew", "${uploadArchives.getPath()}"
        println("${project.getName()}：打包成功")
    }

    return newVersionCode
}

/**
 * 版本号自增，并返回新到版本号字符串
 * @return 返回自增之后到版本号字符串
 */
def getNewVersionCode(Project project) {
    if (!project.extensions.findByName("pushInfo")) {
        return null
    }
    // 1.拿到当前module的版本号
    def pushInfo = project.extensions.pushInfo
    String versionCode = pushInfo.versionCode
    println "版本号:$versionCode"
    def isRelease = pushInfo.isRelease
    println "isRelease=" + isRelease
    // 2.拿到字符串版本的细化版本，如：1.0.0-SNAPSHOT，需要拿到06，把06自增之后，在拼接成：2.0.0.07-SNAPSHOT
    String[] vers = versionCode.split("-")
    println "vers=" + vers
    if (vers != null && vers.length > 0) {
        String verNum = vers[0];
        String verEnd
        if (vers.length > 1) {
            verEnd = vers[1]
            println "verEnd:$verEnd"
        }
        int lastIndex = verNum.lastIndexOf(".")
        println "lastIndex:$lastIndex"
        String lastCode = verNum.substring(lastIndex + 1, verNum.length())
        println "lastCode:$lastCode"

        int lastCodeNum = Integer.parseInt(lastCode)
        println "lastCodeNum=$lastCodeNum"
        String newVersionCode;
        if (++lastCodeNum > 9) {
            newVersionCode = verNum.substring(0, lastIndex + 1) + lastCodeNum
            println "newVersionCode:$newVersionCode"
        } else { // 拼成两位数，如：08
            newVersionCode = verNum.substring(0, lastIndex + 1) + "0$lastCodeNum"
            println "newVersionCode:$newVersionCode"
        }
        // 追加后缀
        if (verEnd != null && !verEnd.isEmpty()) {
            newVersionCode += "-$verEnd"
            println "newVersionCode:$newVersionCode"
        }
        return newVersionCode
    }
    return null
}

/**
 * 修改maven.gradle中的版本号
 * @param newVersionCode 新版本号
 * @return
 */
def updateMavenFile(Project project, String newVersionCode) {
    File file = file("$project.projectDir/maven.gradle")
    // 把文件内容都遍历处理，并存入到StringBuilder中
    updateFileContent(file, { line ->
        return line.trim().startsWith("versionCode") // 以versionCode开头的行
    }, { line ->
        int endIndex = line.indexOf("versionCode")
        // 替换以versionCode开头的行的内容
        return "${line.substring(0, endIndex)}versionCode '$newVersionCode'"
    })
}

/**
 * 更新库的依赖
 * @param project 引入基础库的工程
 * @param oldDependence 老版本
 * @param newDependence 新版本
 * @return
 */
def updateDependencies(Project project, Map<String, String> map) {
    // 获取build.gradle文件
    File file = file("$project.projectDir/build.gradle")
    // 遍历build.gradle文件，并更新依赖库的版本
    def lines = file.readLines()
    file.withWriter { writer ->
        lines.eachWithIndex { String line, int lineIndex ->
            if (lineIndex != 0) {
                writer.write("\n")
            }
            if (line.trim().startsWith("api")) {//  如果是以api开头的行
                map.each { item ->
                    if (line.contains(item.key)) {
                        line = line.replace(item.key, item.value)
                    }
                }
            }
            writer.write("$line")
        }
    }
}

/**
 * 更新文件内存
 * @param file 需要更新的文件
 * @param filter 过滤符合要求的行，满足该函数就替换该行
 * @param newLine 生成需要替换的行的新内容的函数
 * @return
 */
def updateFileContent(File file, Closure filter, Closure newLine) {
    if (file == null) {
        return
    }

    def lines = file.readLines()
    file.withWriter { writer ->
        lines.eachWithIndex { String line, int lineIndex ->
            if (lineIndex != 0) {
                writer.write("\n")
            }
            if (filter.call(line)) {
                writer.write("${newLine.call(line)}")
            } else {
                writer.write("$line")
            }
        }
    }
}
```
