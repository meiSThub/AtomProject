### gradle的执行流程
gradle的执行，分为三个阶段：Initialization初始化阶段，Configuration配置阶段，Execution执行阶段
* Initialization初始化阶段：解析整个工程中所有的Project，构建所有的Project对应的project对象
* Configuration配置阶段：解析所有的project对象中的task，构建好所有的task拓扑图（即有向无环图）
* Execution执行阶段：执行具体的task及其依赖task

![gradle执行流程](https://raw.githubusercontent.com/meiSThub/AtomProject/master/image/pic_6.png)

#### 初始化阶段
初始化阶段的任务是创建项目的层次结构，并且为每一个项目创建一个Project实例。
与初始化阶段相关的脚本文件是 **settings.gradle**（包括<USER_HOME>/.gradle/init.d目录下的所有.gradle脚本文件，这些文件作用于本机的所有构建过程）。一个settings.gradle脚本对应一个Settings对象，我们最常用来声明项目的层次结构的include就是Settings类下的一个方法，在Gradle初始化的时候会构造一个Settings实例对象，它包含了下图中的方法，这些方法都可以直接在settings.gradle中直接访问。
![gradle执行流程](https://raw.githubusercontent.com/meiSThub/AtomProject/master/image/pic_7.png)

``` java
gradle.addBuildListener(new BuildListener() {
  void buildStarted(Gradle var1) {
    println '开始构建'
  }
  void settingsEvaluated(Settings var1) {
    println 'settings评估完成（settins.gradle中代码执行完毕）'
    // var1.gradle.rootProject 这里访问Project对象时会报错，还未完成Project的初始化
  }
  void projectsLoaded(Gradle var1) {
    println '项目结构加载完成（初始化阶段结束）'
    println '初始化结束，可访问根项目：' + var1.gradle.rootProject
  }
  void projectsEvaluated(Gradle var1) {
    println '所有项目评估完成（配置阶段结束）'
  }
  void buildFinished(BuildResult var1) {
    println '构建结束 '
  }
})
```

执行gradle build，打印结果如下：

``` java
settings评估完成（settins.gradle中代码执行完毕）
项目结构加载完成（初始化阶段结束）
初始化结束，可访问根项目：root project 'GradleTest'
所有项目评估完成（配置阶段结束）
:buildEnvironment

------------------------------------------------------------
Root project
------------------------------------------------------------

classpath
No dependencies

BUILD SUCCESSFUL

Total time: 0.959 secs
构建结束

```

#### 配置阶段

配置阶段的任务是执行各项目下的build.gradle脚本，完成Project的配置，并且构造Task任务依赖关系图以便在执行阶段按照依赖关系执行Task。
该阶段也是我们最常接触到的构建阶段，比如应用外部构建插件apply plugin: 'com.android.application'，配置插件的属性android{ compileSdkVersion 25 ...}等。每个build.gralde脚本文件对应一个Project对象，在初始化阶段创建，Project的接口文档。
配置阶段执行的代码包括build.gralde中的各种语句、闭包以及Task中的配置段语句，在根目录的build.gradle中添加如下代码：

``` java
println 'build.gradle的配置阶段'

// 调用Project的dependencies(Closure c)声明项目依赖
dependencies {
    // 闭包中执行的代码
    println 'dependencies中执行的代码'
}

// 创建一个Task
task test() {
  println 'Task中的配置代码'
  // 定义一个闭包
  def a = {
    println 'Task中的配置代码2'
  }
  // 执行闭包
  a()
  doFirst {
    println '这段代码配置阶段不执行'
  }
}

println '我是顺序执行的'

```
调用gradle build，得到如下结果：
``` java
build.gradle的配置阶段
dependencies中执行的代码
Task中的配置代码
Task中的配置代码2
我是顺序执行的
:buildEnvironment

------------------------------------------------------------
Root project
------------------------------------------------------------

classpath
No dependencies

BUILD SUCCESSFUL

Total time: 1.144 secs

```
**一定要注意，配置阶段不仅执行build.gradle中的语句，还包括了Task中的配置语句。**从上面执行结果中可以看到，在执行了dependencies的闭包后，直接执行的是任务test中的配置段代码（Task中除了Action外的代码段都在配置阶段执行）。
另外一点，无论执行Gradle的任何命令，初始化阶段和配置阶段的代码都会被执行。
同样是上面那段Gradle脚本，我们执行帮助任务gradle help，任然会打印出上面的执行结果。我们在排查构建速度问题的时候可以留意，是否部分代码可以写成任务Task，从而减少配置阶段消耗的时间。

#### 执行阶段
在配置阶段结束后，Gradle会根据任务Task的依赖关系创建一个有向无环图，可以通过Gradle对象的getTaskGraph方法访问，对应的类为TaskExecutionGraph，然后通过调用gradle <任务名>执行对应任务。

下面我们展示如何调用子项目中的任务。

1. 在根目录下创建目录subproject，并添加文件build.gradle
2. 在settings.gradle中添加include ':subproject'
3. 在subproject的build.gradle中添加如下代码

``` java
task grandpa {
  doFirst {
    println 'task grandpa：doFirst 先于 doLast 执行'
  }
  doLast {
    println 'task grandpa：doLast'
  }
}

task father(dependsOn: grandpa) {
  doLast {
    println 'task father：doLast'
  }
}

task mother << {
  println 'task mother 先于 task father 执行'
}

task child(dependsOn: [father, mother]){
  doLast {
    println 'task child 最后执行'
  }
}

task nobody {
  doLast {
    println '我不执行'
  }
}
// 指定任务father必须在任务mother之后执行
father.mustRunAfter mother

```

它们的依赖关系如下：

``` java
:subproject:child
+--- :subproject:father
|    \--- :subproject:grandpa
\--- :subproject:mother

```

执行gradle :subproject:child，得到如下打印结果：

``` java
:subproject:mother
task mother 先于 task father 执行
:subproject:grandpa
task grandpa：doFirst 先于 doLast 执行
task grandpa：doLast
:subproject:father
task father：doLast
:subproject:child
task child 最后执行

BUILD SUCCESSFUL

Total time: 1.005 secs

```

因为在配置阶段，我们声明了任务mother的优先级高于任务father，所以mother先于father执行，而任务father依赖于任务grandpa，所以grandpa先于father执行。任务nobody不存在于child的依赖关系中，所以不执行。

参考博客：https://juejin.im/post/5afec54951882542715001f2
https://www.heqiangfly.com/2016/03/18/development-tool-gradle-lifecycle/
