#### 什么是闭包
闭包，通俗来讲，就是一个代码块
闭包，默认会有一个参数it，如果不想用他的话，可以自己定义一个参数，就会覆盖默认参数了
闭包，默认不管如何，都是有返回值的，如果没有return返回值的话，则默认返回null

``` java

// 定义一个闭包
def closer={
    print("hello ${it}")// 默认的隐含参数it
}
closer();// 闭包的调用 ，输出：hello null
closer("groovy") // 输出：hello groovy

def closer={ name ->// 只有一个参数，则参数name会覆盖隐藏参数it
    print("hello ${name}")// 默认的隐含参数it
}
closer();// 闭包的调用 ，输出：hello null
closer("groovy") // 输出：hello groovy

def closer={String name ,int age->// 只有一个参数，则参数name会覆盖隐藏参数it
    print("hello ${name}, age is ${age}")// 默认的隐含参数it
}
closer("groovy") // 输出：hello groovy, age is 10

// 闭包还可以通过call方法调用，即闭包的call方法
closer.call("groovy",10)

```

#### int类型与闭包的结合使用
``` java
// 计算阶乘方法
int fab(int  number){
    int result=1;
    1.upto(number,{num-> result=result*num});
    return result;
}

int x= fab(5)

println x

int  cal(int  number){
    int result=0;
    number.times {
        num->result+=num
    }
    return result
}

```


#### 字符串与闭包结合使用

``` java
// 闭包与字符串的结合使用
def str = "the 2 and 3 is 5"

// each闭包，遍历字符串的每个字符
println str.each {
    print(it)
} // 输出：the 2 and 3 is 5the 2 and 3 is 5

// find闭包，查找符合条件的第一个
println str.find{s->
    s.isNumber() // 输出：2
}

// findAll闭包，查找所有符合条件的
def list= str.findAll{
    s-> s.isNumber()
}
println list.toListString() // 输出：[2, 3, 5]

// any闭包，判断是否包含符合条件的
println str.any{s-> // 输出：true
    s.isNumber()
}

// every闭包，判断是否都符合条件
println str.every { s-> // 输出：false
        s.isNumber()
}

// collect闭包，把字符串中的每个字符都按给定的操作处理
def list2=str.collect{s->
    s.toUpperCase()
}
println list2.toListString()  // 输出：[T, H, E,  , 2,  , A, N, D,  , 3,  , I, S,  , 5]
```

#### Groovy 闭包进阶讲解
1. 闭包的关键字（关键变量）
  * this
  * owner
  * delegate


1.闭包的关键值
``` java
/**
 * 闭包的三个重要变量：this，owner，delegate
 */

def scriptCloser={
    println "scriptCloser this:"+this // 代表闭包定义处的类，
    println "scriptCloser owner:"+owner // 代表闭包定义处的类或者对象
    println "scriptCloser delegate:"+delegate// 代表任意对象，默认与owner一致
}
scriptCloser.call()
/** 输出：
 * scriptCloser this:com.plum.groovy.variable.StructStudy@4975dda1
 * scriptCloser owner:com.plum.groovy.variable.StructStudy@4975dda1
 * scriptCloser delegate:com.plum.groovy.variable.StructStudy@4975dda1
 */
println("---------------------\n")

class Person{
    def static classCloser={
        println "classCloser this:"+this // 代表闭包定义处的类，
        println "classCloser owner:"+owner // 代表闭包定义处的类或者对象
        println "classCloser delegate:"+delegate// 代表任意对象，默认与owner一致
    }

    def static say(){
        def classCloser={
            println "methodClassCloser this:"+this // 代表闭包定义处的类，
            println "methodClassCloser owner:"+owner // 代表闭包定义处的类或者对象
            println "methodClassCloser delegate:"+delegate// 代表任意对象，默认与owner一致
        }
        classCloser.call()
    }
}

Person.classCloser.call()
Person.say()
/**输出：代表的是类
 * classCloser this:class com.plum.groovy.variable.Person
 * classCloser owner:class com.plum.groovy.variable.Person
 * classCloser delegate:class com.plum.groovy.variable.Person
 * methodClassCloser this:class com.plum.groovy.variable.Person
 * methodClassCloser owner:class com.plum.groovy.variable.Person
 * methodClassCloser delegate:class com.plum.groovy.variable.Person
 */

println("----\n")

// 闭包中定义闭包
def nestCloser={
    def innerCloser={
        println "innerClassCloser this:"+this // 代表的是StructStudy对象
        println "innerClassCloser owner:"+owner // 代表是nestCloser闭包对象
        println "innerClassCloser delegate:"+delegate// 代表是nestCloser闭包对象
    }
    innerCloser.delegate =new Person()
    innerCloser.call()
}

nestCloser.call()
/** 输出：
 * innerClassCloser this:com.plum.groovy.variable.StructStudy@2e362407
 * innerClassCloser owner:com.plum.groovy.variable.StructStudy$_run_closure9@2a5abd3c
 * innerClassCloser delegate:com.plum.groovy.variable.StructStudy$_run_closure9@2a5abd3c
 */
```
总结：
1. 在大多数情况下，this，owner，delegate所指向的类或对象一致
2. 我们在闭包中，定义一个闭包的时候，this指向的是当前类或者对象，owner和delegate所指向的是外层闭包
3. 如果我们修改类delegate的值的话，那么delegate则与owner和this的值不一样
4. 即this，owner值不可修改，delegate值可以修改

#### 闭包中的委托策略
``` java
/**
 * 闭包的委托策略
 * */

class Student{
    String name
    def pretty={"my name is ${name}"}

    @Override
    String toString() {
        pretty.call()
    }
}

class Teacher{
    String name
    String name1
}

def stu=new Student(name: "Search")
def tea=new Teacher(name: "Mr Wang")
println stu.toString() // 输出：my name is Search
// 修改代理对象
stu.pretty.delegate = tea
//println stu.toString() // 输出：my name is Search
// 修改代理策略
stu.pretty.resolveStrategy = Closure.DELEGATE_FIRST // 委托优先，即闭包先去委托中找，如果找到了name，则选用，没有则又取本身的
// 委托优先,即先从委托：delegate中去寻找变量和方法，如果没有找到，则去owner中寻找
println stu.toString() // 输出：my name is Mr Wang
// 说明在修改闭包的delegate对象
stu.pretty.resolveStrategy = Closure.DELEGATE_ONLY // 只在委托对象中找，找不到，则报错
println stu.toString()

/**
* 委托策略总结：
 * 1. Closure.DELEGATE_FIRST，委托优先：即先从委托：delegate中去寻找变量和方法，如果没有找到，则去owner中寻找
 * 2. Closure.DELEGATE_ONLY,只在委托对象中找，即只在委托对象delegate中寻找变量和方法，如果没有找到，则报错
* */
```
闭包中的委托策略总结：
1. Closure.DELEGATE_FIRST，委托优先：即先从委托：delegate中去寻找变量和方法，如果没有找到，则去owner中寻找
2. Closure.DELEGATE_ONLY,只在委托对象中找，即只在委托对象delegate中寻找变量和方法，如果没有找到，则报错
3. OWNER_ONLY,只在owner对象中找
4. OWNER_FIRST,owner优先，
